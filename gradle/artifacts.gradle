def packagesToRelocate = ['javassist', 'org.glassfish', 'org.jvnet', 'jersey.repackaged',
        'com.fasterxml', 'io.netty', 'org.bouncycastle', 'org.apache', 'org.aopalliance',
        'org.scijava', 'com.google', 'javax.annotation', 'javax.inject', 'javax.ws']
    
shadowJar {
    from rootProject.configurations.findByName('compileOnly')
    mergeServiceFiles()
    def basePackage = "com.github.dockerjava.shaded"
    packagesToRelocate.each { pkg ->
        relocate pkg, "${basePackage}.${pkg}"
    }
}

jar {
    dependsOn shadowJar
    duplicatesStrategy = 'exclude'
    manifest {
        attributes 'Implementation-Title': "${project.name}",
                   'Implementation-Version': "${->project.version}",
                   'Built-By': System.getProperty('user.name'),
                   'Built-Date': new Date(),
                   'Built-JDK': System.getProperty('java.version'),
                   'Built-Gradle': gradle.gradleVersion
    }
    from zipTree(shadowJar.outputs.files.singleFile)
}
